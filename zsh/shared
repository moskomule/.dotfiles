#!/local/bin/env zsh
#
bindkey -e
# avoid nomatch error when *, []
setopt nonomatch 
# enable # comments in interactive mode
setopt interactivecomments
# cd -[tab] suggests directories
setopt auto_pushd
setopt pushd_ignore_dups

setopt extended_glob
#setopt prompt_subst

# completion
autoload -U compinit; compinit
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
setopt complete_aliases

# history
setopt share_history
setopt extended_history 
export HISTFILE=${HOME}/.histfile
setopt hist_ignore_all_dups
export HISTSIZE=10000 # on memory
export SAVEHIST=10000
disable r # use ctrl+r instead


# PATH
export PATH="${PATH}:${HOME}/.dotfiles/bin"

# see http://www.sirochro.com/note/terminal-zsh-prompt-customize/
autoload -Uz colors # black red green yellow blue magenta cyan white
colors

# set aliases or replace commands
alias ls='ls -GF'
alias la='ls -la'
alias lh='ls -lh'

if (( $+commands[jupyter] )); then
    alias jup='jupyter lab'
fi

if (( $+commands[direnv] )); then
    # recommended
    alias tmux='direnv exec / tmux'
fi
alias tma='tmux a'

if (( $+commands[nvim] )); then
    alias vim='nvim'
fi

if (( $+commands[bat] )); then
    alias cat='bat'
    # man with highlight
    export MANPAGER="sh -c 'col -bx | bat -l man -p'"
fi

if (( $+commands[exa] )); then
    alias ls='exa --git'
fi

if (( $+commands[hub] )); then
    alias git='hub'
fi

# functions
mc(){
    mkdir $1
    cd $1
    echo `pwd`
}

if (( $+commands[ghq] )); then
    gg(){
        URL=$1
        ghq get ${URL}
        # # refers to the start and % refers to the end
        cd "$(ghq root)/${${URL#*//}%.git}"
    }

fi

# filter can be fzf
FILTER=${FILTER:-fzf}

if (( $+commands[${FILTER}] )); then
    alias -g P='| ${FILTER}'
    function filter-select-history() {
        BUFFER=$(fc -l -r -n 1 | ${FILTER} --query "$LBUFFER")
        CURSOR=$#BUFFER
        zle redisplay
    }
    zle -N filter-select-history
    bindkey '^r' filter-select-history
    
    if (( $+commands[ghq] )); then
        function filter-src() {
            local selected_dir=$(ghq list | ${FILTER} --query "$LBUFFER")
            if [ -n "$selected_dir" ]; then
                BUFFER="cd $(ghq root)/${selected_dir}"
                zle accept-line
            fi
            zle redisplay
        }
        zle -N filter-src
        bindkey '^g' filter-src
    fi
fi

if (( $+commands[fzf] )); then
    export FZF_DEFAULT_OPTS='--height 40% --reverse --border'
    if (( $+commands[rg] )); then
        export FZF_DEFAULT_COMMAND='rg --files --hidden --follow --glob "!.git/*"'
    fi
    if (( $+commands[bat] )); then
        alias fzfp=fzf --preview "bat  --color=always --style=header,grid  {}"
    fi
fi

if (( $+commands[direnv] )); then
    export EDITOR=nvim
    eval "$(direnv hook zsh)"
fi

# plugins
fpath=( /usr/local/share/zsh-completions $fpath )
#fpath=( "${HOME}/.dotfiles/zsh" $fpath )

fpath+=${HOME}/.dotfiles/zsh/pure
autoload -U promptinit; promptinit
prompt pure
source ${HOME}/.dotfiles/zsh/zsh-autosuggestions/zsh-autosuggestions.zsh
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=250'

if [[ ! $(uname) != "Darwin" ]]; then
    # to avoid pure's problem
    if [[ "$LANG" == "" ]]; then
        export LANG=en_US.UTF-8
    fi
    
    if [[ "$LC_CTYPE" == "" ]]; then
        export LC_CTYPE=en_US.UTF-8
    fi
fi
